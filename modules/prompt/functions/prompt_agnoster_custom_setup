# vim:ft=zsh ts=2 sw=2 sts=2
#
# Originals:
# - agnoster's Theme - https://gist.github.com/3712874
# - oh-my-zsh agnoster Theme - https://github.com/robbyrussell/oh-my-zsh/blob/master/themes/agnoster.zsh-theme
#
# A Powerline-inspired theme for ZSH
#
# # README
#
# In order for this theme to render correctly, you will need a
# [Powerline-patched font](https://github.com/Lokaltog/powerline-fonts).
# Make sure you have a recent version: the code points that Powerline
# uses changed in 2012, and older versions will display incorrectly,
# in confusing ways.
#
# In addition, I recommend the
# [Solarized theme](https://github.com/altercation/solarized/) and, if you're
# using it on Mac OS X, [iTerm 2](http://www.iterm2.com/) over Terminal.app -
# it has significantly better color fidelity.
#
# # Goals
#
# The aim of this theme is to only show you *relevant* information. Like most
# prompts, it will only show git information when in a git working directory.
# However, it goes a step further: everything from the current user and
# hostname to whether the last call exited with an error to whether background
# jobs are running in this shell will all be displayed automatically when
# appropriate.

### Segment drawing
# A few utility functions to make it easy and re-usable to draw segmented prompts

CURRENT_BG='NONE'

# Special Powerline characters

() {
  local LC_ALL="" LC_CTYPE="en_US.UTF-8"
  # NOTE: This segment separator character is correct.  In 2012, Powerline changed
  # the code points they use for their special characters. This is the new code point.
  # If this is not working for you, you probably have an old version of the
  # Powerline-patched fonts installed. Download and install the new version.
  # Do not submit PRs to change this unless you have reviewed the Powerline code point
  # history and have new information.
  # This is defined using a Unicode escape sequence so it is unambiguously readable, regardless of
  # what font the user is viewing this source code in. Do not replace the
  # escape sequence with a single literal character.
  # Do not change this! Do not make it '\u2b80'; that is the old, wrong code point.
  SEGMENT_SEPARATOR=$'\ue0b0'
}

# Begin a segment
# Takes two arguments, background and foreground. Both can be omitted,
# rendering default background/foreground.
prompt_agnoster_custom_segment() {
  local bg fg
  [[ -n $1 ]] && bg="%K{$1}" || bg="%k"
  [[ -n $2 ]] && fg="%F{$2}" || fg="%f"
  if [[ $CURRENT_BG != 'NONE' && $1 != $CURRENT_BG ]]; then
    echo -n " %{$bg%F{$CURRENT_BG}%}$SEGMENT_SEPARATOR%{$fg%} "
  else
    echo -n "%{$bg%}%{$fg%} "
  fi
  CURRENT_BG=$1
  [[ -n $3 ]] && echo -n $3
}

# End the prompt, closing any open segments
prompt_agnoster_custom_end() {
  if [[ -n $CURRENT_BG ]]; then
    echo -n " %{%k%F{$CURRENT_BG}%}$SEGMENT_SEPARATOR"
  else
    echo -n "%{%k%}"
  fi
  echo -n "%{%f%}"
  CURRENT_BG=''
}

### Prompt components
# Each component will draw itself, and hide itself if no information needs to be shown

# Context: user@hostname (who am I and where am I)
prompt_agnoster_custom_context() {
  if [[ "$USER" != "$DEFAULT_USER" || -n "$SSH_CLIENT" ]]; then
    prompt_agnoster_custom_segment black default "%(!.%{%F{yellow}%}.)$USER@%m"
  fi
}

# Git: branch/detached head, dirty status
prompt_agnoster_custom_git() {

  local PL_BRANCH_CHAR
  () {
    local LC_ALL="" LC_CTYPE="en_US.UTF-8"
    PL_BRANCH_CHAR=$'\ue0a0'         # 
  }
  local ref dirty mode repo_path
  repo_path=$(git rev-parse --git-dir 2>/dev/null)

  if $(git rev-parse --is-inside-work-tree >/dev/null 2>&1); then
    dirty=$(git status --ignore-submodules=dirty --porcelain --untracked-files=no 2> /dev/null | tail -n1)
    ref=$(git symbolic-ref HEAD 2> /dev/null) || ref="➦ $(git rev-parse --short HEAD 2> /dev/null)"
    if [[ -n $dirty ]]; then
      prompt_agnoster_custom_segment yellow black
    else
      prompt_agnoster_custom_segment green black
    fi

    if [[ -e "${repo_path}/BISECT_LOG" ]]; then
      mode=" <B>"
    elif [[ -e "${repo_path}/MERGE_HEAD" ]]; then
      mode=" >M<"
    elif [[ -e "${repo_path}/rebase" || -e "${repo_path}/rebase-apply" || -e "${repo_path}/rebase-merge" || -e "${repo_path}/../.dotest" ]]; then
      mode=" >R>"
    fi

    setopt promptsubst
    autoload -Uz vcs_info

    zstyle ':vcs_info:*' enable git
    zstyle ':vcs_info:*' get-revision true
    zstyle ':vcs_info:*' check-for-changes true
    zstyle ':vcs_info:*' stagedstr '✚'
    zstyle ':vcs_info:*' unstagedstr '●'
    zstyle ':vcs_info:*' formats ' %u%c'
    zstyle ':vcs_info:*' actionformats ' %u%c'
    vcs_info
    echo -n "${ref/refs\/heads\//$PL_BRANCH_CHAR }${vcs_info_msg_0_%% }${mode}"
  fi
}

prompt_agnoster_custom_hg() {
  local rev status
  if $(hg id >/dev/null 2>&1); then
    if $(hg prompt >/dev/null 2>&1); then
      if [[ $(hg prompt "{status|unknown}") = "?" ]]; then
        # if files are not added
        prompt_agnoster_custom_segment red white
        st='±'
      elif [[ -n $(hg prompt "{status|modified}") ]]; then
        # if any modification
        prompt_agnoster_custom_segment yellow black
        st='±'
      else
        # if working copy is clean
        prompt_agnoster_custom_segment green black
      fi
      echo -n $(hg prompt "☿ {rev}@{branch}") $st
    else
      st=""
      rev=$(hg id -n 2>/dev/null | sed 's/[^-0-9]//g')
      branch=$(hg id -b 2>/dev/null)
      if `hg st | grep -q "^\?"`; then
        prompt_agnoster_custom_segment red black
        st='±'
      elif `hg st | grep -q "^[MA]"`; then
        prompt_agnoster_custom_segment yellow black
        st='±'
      else
        prompt_agnoster_custom_segment green black
      fi
      echo -n "☿ $rev@$branch" $st
    fi
  fi
}

# Dir: current working directory
prompt_agnoster_custom_dir() {
  prompt_agnoster_custom_segment blue black '%~'
}

# Virtualenv: current working virtualenv
prompt_agnoster_custom_virtualenv() {
  local virtualenv_path="$VIRTUAL_ENV"
  if [[ -n $virtualenv_path && -n $VIRTUAL_ENV_DISABLE_PROMPT ]]; then
    prompt_agnoster_custom_segment blue black "(`basename $virtualenv_path`)"
  fi
}

# Status:
# - was there an error
# - am I root
# - are there background jobs?
prompt_agnoster_custom_status() {
  local symbols
  symbols=()
  [[ $RETVAL -ne 0 ]] && symbols+="%{%F{red}%}✘"
  [[ $UID -eq 0 ]] && symbols+="%{%F{yellow}%}⚡"
  [[ $(jobs -l | wc -l) -gt 0 ]] && symbols+="%{%F{cyan}%}⚙"

  [[ -n "$symbols" ]] && prompt_agnoster_custom_segment black default "$symbols"
}

# ---
# Extensions to the theme starts here.
# ---
() {
  local LC_ALL="" LC_CTYPE="en_US.UTF-8"
  RSEGMENT_SEPARATOR=$'\ue0b2'
}

rprompt_agnoster_custom_segment() {
  local bg fg rbg rfg
  [[ -n $1 ]] && bg="%K{$1}" || bg="%k"
  [[ -n $1 ]] && rbg="%F{$1}" || rbg="%f"
  [[ -n $2 ]] && fg="%F{$2}" || fg="%f"
  [[ -n $2 ]] && rfg="%F{$2}" || rfg="%f"
  if [[ $RCURRENT_BG != 'NONE' && $1 != $RCURRENT_BG ]]; then
    echo -n " %{%K{$RCURRENT_BG}$rbg%}$RSEGMENT_SEPARATOR%{$fg$bg%} "
  else
    echo -n "%{%k$rbg%}$RSEGMENT_SEPARATOR%{$bg$fg%} "
  fi
  RCURRENT_BG=$1
  [[ -n $3 ]] && echo -n $3
}

# End the prompt, closing any open segments
rprompt_agnoster_custom_end() {
  if [[ -n $RCURRENT_BG ]]; then
    echo -n "%{%K{$RCURRENT_BG}%} "
  fi
  echo -n "%{%k%f%}"
  RCURRENT_BG=''
}

prompt_agnoster_custom_main() {
  RETVAL=$?
  CURRENT_BG='NONE'
  prompt_agnoster_custom_status
  prompt_agnoster_custom_context
  prompt_agnoster_custom_git
  prompt_agnoster_custom_hg
  prompt_agnoster_custom_virtualenv
  prompt_agnoster_custom_end
}

prompt_agnoster_custom_reverse() {
  RCURRENT_BG='NONE'
  rprompt_agnoster_custom_segment black default '%~'
  rprompt_agnoster_custom_end
}

prompt_agnoster_custom_precmd() {
  PROMPT='%{%f%b%k%}$(prompt_agnoster_custom_main) '
  RPROMPT='%{%f%b%k%}$(prompt_agnoster_custom_reverse)'
}

prompt_agnoster_custom_setup() {
  autoload -Uz add-zsh-hook

  prompt_opts=(cr subst percent)

  add-zsh-hook precmd prompt_agnoster_custom_precmd
}

prompt_agnoster_custom_setup "$@"
